
plugins {
    id 'org.springframework.boot' version '3.5.4' apply false
    id 'io.spring.dependency-management' version '1.1.6' apply false
    id 'com.diffplug.spotless' version '7.2.1' apply false
}

allprojects {  
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.diffplug.spotless'

    group = 'com.mizegret.mps'
    version = '0.0.1-SNAPSHOT'

    java {
        sourceCompatibility = JavaVersion.VERSION_21
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'org.springframework.boot:spring-boot-starter-aop'
        developmentOnly 'org.springframework.boot:spring-boot-devtools'
        compileOnly 'org.projectlombok:lombok'
        // https://stackoverflow.com/questions/53326271/spring-nullable-annotation-generates-unknown-enum-constant-warning
        compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    tasks.named('test') {
	    useJUnitPlatform()
    }

    spotless {
        java {
            googleJavaFormat()
            trimTrailingWhitespace()
            endWithNewline()
        }
    }

    // Enforce formatting on 'check' so 'build' will fail if not formatted
    tasks.named('check') {
        dependsOn 'spotlessCheck'
    }
}
